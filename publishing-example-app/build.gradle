plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'org.jlleitschuh.gradle.ktlint'
}

final useCrashlytics = project.file('google-services.json').exists()
if (useCrashlytics) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}

android {
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId 'com.ably.tracking.example.publisher'
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        // Required to initialize Mapbox and use MapView in the layout XML files.
        resValue "string", "mapbox_access_token", findProperty('MAPBOX_ACCESS_TOKEN') ?: ""
    }
    namespace 'com.ably.tracking.example.publisher'
}

dependencies {
    implementation project(':publishing-sdk')
    implementation files('./../lib/aar-main.aar')

    implementation 'org.msgpack:msgpack-core:0.8.11'
    implementation 'org.java-websocket:Java-WebSocket:1.5.3'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.davidehrmann.vcdiff:vcdiff-core:0.1.1'

    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.amplifyframework:aws-storage-s3:1.4.1'
    implementation 'com.amplifyframework:aws-auth-cognito:1.4.1'
    implementation 'pub.devrel:easypermissions:3.0.0'
    // This version needs to be compatible with the "com.mapbox.navigation:core" dependency version from publishing-sdk.
    implementation 'com.mapbox.maps:android:10.8.0'

    if (useCrashlytics) {
        // The BoM for the Firebase platform (it specifies the versions for Firebase dependencies).
        implementation platform('com.google.firebase:firebase-bom:26.8.0')

        // Firebase dependencies, which should not specifies versions as those are provided
        // for us because we're including the BoM above.
        // see: https://firebase.google.com/docs/crashlytics/get-started?platform=android
        implementation 'com.google.firebase:firebase-crashlytics'
    }
}
