// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.3.72"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'org.jlleitschuh.gradle.ktlint' version '9.4.1'
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = property('MAPBOX_DOWNLOADS_TOKEN')
            }
        }
    }
}

subprojects {
    // We have to configure android in afterEvaluate because, otherwise, we get a Gradle error.
    // https://stackoverflow.com/a/21032272
    // Same goes for testImplementation() method.
    afterEvaluate { evaluatedSubProject ->
        final evaluatedSubProjectIsALibrary = evaluatedSubProject.pluginManager.hasPlugin('com.android.library')
        final evaluatedSubProjectIsAnApp = evaluatedSubProject.pluginManager.hasPlugin('com.android.application')
        final evaluatedSubProjectIsKotlin = evaluatedSubProject.pluginManager.hasPlugin('kotlin-android')

        android {
            compileSdkVersion 30
            buildToolsVersion "30.0.2"

            defaultConfig {
                minSdkVersion 19
                targetSdkVersion 30

                // We have no reason not to match versions assigned to all artifacts generated by
                // projects in this repository. Therefore, this same version number is used for SDK and
                // example app projects alike.
                versionCode 1
                versionName "1.0"

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

                if (evaluatedSubProjectIsALibrary) {
                    consumerProguardFiles "consumer-rules.pro"
                }
                if (evaluatedSubProjectIsAnApp) {
                    multiDexEnabled true
                }
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            if (evaluatedSubProjectIsKotlin) {
                kotlinOptions {
                    jvmTarget = '1.8'
                }
            }

            // https://developer.android.com/reference/tools/gradle-api/4.1/com/android/build/api/dsl/LintOptions
            // http://tools.android.com/tips/lint-checks
            lintOptions {
                warningsAsErrors true
                checkTestSources true

                // More accessible output, when run from command line and in CI/CD environments.
                textReport true
                textOutput 'stdout'
                explainIssues false

                // Lint rules which we might care about at some point, but need not fail the build.
                informational 'GradleDependency',
                              'NewerVersionAvailable',
                              'AllowBackup',
                              'StopShip'

                // Lint rules which are disabled by default (see: lint --show), but which we would
                // rather were enabled.
                error 'MissingRegistered',
                      'Registered',
                      'WrongThreadInterprocedural',
                      'MinSdkTooLow',
                      'MangledCRLF',
                      'EasterEgg',
                      'UnpackedNativeCode',
                      'LogConditional',
                      'UnusedIds'
            }

            // https://github.com/jlleitschuh/ktlint-gradle#configuration
            ktlint {
                version = '0.39.0'
                verbose = true
                android = true
                outputToConsole = true
            }

            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }
        }

        dependencies {
            implementation 'androidx.appcompat:appcompat:1.2.0'
            implementation 'com.google.android.material:material:1.2.1'
            implementation 'com.jakewharton.timber:timber:4.7.1'

            if (evaluatedSubProjectIsKotlin) {
                implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
                implementation 'androidx.core:core-ktx:1.3.2'

                testImplementation 'io.mockk:mockk:1.10.2'
            }

            if (evaluatedSubProjectIsALibrary) {
                implementation 'io.ably:ably-android:1.2.2'
            }

            if (evaluatedSubProjectIsAnApp) {
                implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
                implementation 'androidx.multidex:multidex:2.0.1'
            }

            testImplementation 'junit:junit:4.13.1'

            androidTestImplementation 'androidx.test.ext:junit:1.1.2'
            androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
