apply plugin: 'maven-publish'
apply plugin: 'signing'

final githubActor = findProperty('GITHUB_ACTOR')
final githubToken = findProperty('GITHUB_TOKEN')
final isPublishingToGitHubPackages = findProperty('publishTarget') == 'GitHubPackages'

task sourcesJar(type: Jar) {
    // Copy files from main source directories to the JAR.
    from android.sourceSets.main.java.srcDirs
    // Specify the JAR type (it will be appended to its filename).
    archiveClassifier.set("sources")
}

task dokkaJavadocJar(type: Jar) {
    // Generate the javadocs.
    dependsOn dokkaJavadoc
    // Copy files from the javadocs directory to the jar.
    from dokkaJavadoc.outputDirectory
    // Specify the JAR type (it will be appended to its filename).
    archiveClassifier.set("javadoc")
}

afterEvaluate {
    // Based on: https://developer.android.com/studio/build/maven-publish-plugin
    // See: https://docs.gradle.org/current/dsl/org.gradle.api.publish.PublishingExtension.html
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Add a JAR with source code (to enable viewing the code when the library is included from a maven repository).
                artifact sourcesJar
                // Add a JAR with javadocs.
                artifact dokkaJavadocJar

                // Add POM metadata
                pom {
                    name = PUBLISH_POM_NAME
                    description = PUBLISH_POM_DESCRIPTION
                    url = 'https://github.com/ably/ably-asset-tracking-android'
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://github.com/ably/ably-asset-tracking-android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        // https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPomDeveloper.html
                        developer {
                            id = 'ably' // our company org in GitHub: https://github.com/ably
                            name = 'Ably' // UK based company: Ably Real-time Ltd
                            email = 'support@ably.com'
                            organization = 'Ably' // UK based company: Ably Real-time Ltd
                            organizationUrl = 'https://ably.com/'
                            url = 'https://ably.com/'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/ably/ably-asset-tracking-android.git'
                        developerConnection = 'scm:git:ssh://github.com/ably/ably-asset-tracking-android.git'
                        url = 'https://github.com/ably/ably-asset-tracking-android/tree/main'
                    }
                }
            }
        }

        repositories {
            if (isPublishingToGitHubPackages && githubActor != null && githubToken != null) {
                // per: https://docs.github.com/en/packages/working-with-a-github-packages-registry/working-with-the-gradle-registry
                maven {
                    url = uri("https://maven.pkg.github.com/ably/ably-asset-tracking-android")
                    credentials {
                        username = githubActor
                        password = githubToken
                    }
                }
            }
        }
    }
}

// Sign the artifacts
signing {
    // According to signing plugin docs we should be providing the secret key in the ascii-armored format.
    // This means that the bytes of the key should be converted to a Base64 encoded string.
    // https://docs.gradle.org/current/userguide/signing_plugin.html#using_in_memory_ascii_armored_openpgp_subkeys
    useInMemoryPgpKeys(
        findProperty("SIGNING_KEY_ID"),
        findProperty("SIGNING_KEY_BASE64"),
        findProperty("SIGNING_PASSWORD"),
    )
    sign publishing.publications
}
